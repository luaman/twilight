dnl	$Id$
dnl
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.13)
AC_INIT(nq/sv_main.c)
AC_REVISION($Revision$)

AM_INIT_AUTOMAKE(twilight,0.0.0)
AM_CONFIG_HEADER(include/config.h)
AC_CANONICAL_HOST

dnl Checks for programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

dnl Checks for extensions
AC_EXEEXT
AC_OBJEXT

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_C_CONST

dnl Custom macros (move these to a seperate file?)
AC_DEFUN(TL_CHECK_LIB,[
	LDFLAGS_SAVE=$LDFLAGS
	LDFLAGS="$3 -l$1 $LDFLAGS"
	AC_TRY_LINK_FUNC($2, [$4], [$5])
	LDFLAGS=$LDFLAGS_SAVE
])
AC_DEFUN(TL_CHECK_HEADER,[
	CPPFLAGS_SAVE=$CPPFLAGS
	CPPFLAGS="$2 $CPPFLAGS"
	AC_TRY_CPP([
#include <$1>
], [$3], [$4])
	CPPFLAGS=$CPPFLAGS_SAVE
])

AC_MSG_CHECKING(Checking to see if building on Win32)
case "${host}" in
	*-*-mingw32*)
		SYS=WIN32
		AC_MSG_RESULT(yes)
		;;
# FIXME: What is cygwin's host type?  Check it here.  This is just a guess.
	*-*-*cygwin*)
		SYS=WIN32
		AC_MSG_RESULT(yes)
		;;
	*)
		# Default to POSIX compliant system
		SYS=POSIX
		AC_MSG_RESULT(no)
		;;
esac

AM_CONDITIONAL(SYS_POSIX, test "$SYS" = "POSIX")
AM_CONDITIONAL(SYS_WIN32, test "$SYS" = "WIN32")

AC_CHECK_FUNCS(strlcat strlcpy)

dnl Check for SDL, fatal if not found
AM_PATH_SDL(1.2.0,
	,
	AC_MSG_ERROR([SDL 1.2 or above is required to build $PACKAGE])
)
SDLVER="$sdl_major_version.$sdl_minor_version.$sdl_micro_version"


dnl APIENTRY needs to be defined to nothing except on win32
if test "${SYS}" != "WIN32"; then
	AC_DEFINE_UNQUOTED(APIENTRY,)
fi

dnl let the user choose the default OpenGL library
AC_ARG_WITH(opengl,
	[  --with-opengl           Default name of your OpenGL library
                          (only affects value of the gl_driver Cvar)],,
[if test "$SYS" = "WIN32"; then
	with_opengl="opengl32.dll"
else
	with_opengl="libGL.so.1"
fi
])
AC_DEFINE_UNQUOTED(GL_LIBRARY, "$with_opengl")


dnl sanitize our paths a bit

test "$prefix" = "NONE" && prefix="$ac_default_prefix"
test "x$exec_prefix" = "xNONE" && exec_prefix="$prefix"

eval PDIR="$prefix"
eval DDIR="$datadir"
eval BDIR="$bindir"

dnl userpath/sharepath config
AC_ARG_WITH(sharepath,
	[  --with-sharepath        Default shared data path [DATADIR/games/twilight]],,
	with_sharepath="$DDIR/games/twilight")
AC_DEFINE_UNQUOTED(SHAREPATH, "$with_sharepath")

AC_ARG_WITH(userpath,
	[  --with-userpath         Default user data path [\$HOME/.twilight]],,
	with_userpath="~/.twilight")
AC_DEFINE_UNQUOTED(USERPATH, "$with_userpath")

CFLAGS="-Wall -Werror $CFLAGS"



AC_CHECK_PROG(CVS2CL, cvs2cl, cvs2cl)
AM_CONDITIONAL(CVS2CL, test "x$CVS2CL" != "x")

AC_OUTPUT(
	Makefile
	nq/Makefile
	qw/Makefile
	qw-server/Makefile
)

AC_MSG_RESULT([
  Project Twilight v$VERSION configuration:
    Compiler used               : $CC
    Compiler flags              : $CFLAGS
    SDL version                 : $SDLVER
    Default OpenGL library      : $with_opengl

  Path information
    Installation prefix         : $PDIR
    Binaries install to         : $BDIR
    Shared (read-only) data in  : $with_sharepath
    User (writable) data in     : $with_userpath

You are now ready to run make.
])

