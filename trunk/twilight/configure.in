dnl	$Id$
dnl
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.13)
AC_INIT(nq/sv_main.c)
AC_REVISION($Revision$)

AM_INIT_AUTOMAKE(twilight,0.0.0)
AM_CONFIG_HEADER(include/config.h)

dnl Checks for programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_C_CONST

dnl Custom macros (move these to a seperate file?)
AC_DEFUN(TL_CHECK_LIB,[
	LDFLAGS_SAVE=$LDFLAGS
	LDFLAGS="$3 -l$1 $LDFLAGS"
	AC_TRY_LINK_FUNC($2, [$4], [$5])
	LDFLAGS=$LDFLAGS_SAVE
])
AC_DEFUN(TL_CHECK_HEADER,[
	CPPFLAGS_SAVE=$CPPFLAGS
	CPPFLAGS="$2 $CPPFLAGS"
	AC_TRY_CPP([
#include <$1>
], [$3], [$4])
	CPPFLAGS=$CPPFLAGS_SAVE
])


dnl Check for SDL, fatal if not found
AM_PATH_SDL(1.2.0,
	,
	AC_MSG_ERROR([SDL 1.2 or above is required to build $PACKAGE])
)
SDLVER="$sdl_major_version.$sdl_minor_version.$sdl_micro_version"


dnl APIENTRY needs to be defined to nothing
AC_DEFINE_UNQUOTED(APIENTRY,)

test "$prefix" = "NONE" && prefix="$ac_default_prefix"
test "x$exec_prefix" = "xNONE" && exec_prefix="$prefix"

eval PDIR="$prefix"
eval DDIR="$datadir"
eval BDIR="$bindir"

dnl userpath/sharepath config
AC_ARG_WITH(sharepath,
	[  --with-sharepath        Default shared data path [DATADIR/games/twilight]],,
	with_sharepath="$DDIR/games/twilight")
AC_DEFINE_UNQUOTED(SHAREPATH, "$with_sharepath")

AC_ARG_WITH(userpath,
	[  --with-userpath         Default user data path [\$HOME/.twilight]],,
	with_userpath="~/.twilight")
AC_DEFINE_UNQUOTED(USERPATH, "$with_userpath")

CFLAGS="-Wall -Werror $CFLAGS"

AC_OUTPUT(
	Makefile
	nq/Makefile
	qw/Makefile
	qw-server/Makefile
)

AC_MSG_RESULT([
  Project Twilight v$VERSION configuration:
    Compiler flags              : $CFLAGS
    SDL version                 : $SDLVER

  Path information
    Installation prefix         : $PDIR
    Binaries install to         : $BDIR
    Shared (read-only) data in  : $with_sharepath
    User (writable) data in     : $with_userpath

You are now ready to run make.
])

